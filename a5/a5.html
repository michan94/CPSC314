<html>
  <head>
    <meta charset="UTF-8">
    <title>CPSC 314 Assignment 5 Sep 2019</title>
    <style>
      body { margin: 0; } canvas {width: 100%;
      height: 100% }
    </style>
  </head>
  <body>
    <div id="canvas"></div>
<!--    <script src="js/three.js"></script> -->
    <script src="js/three.min.js"></script>
    <script src="js/OBJLoader.js"></script>
    <script src="js/SourceLoader.js"></script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/KeyboardState.js"></script> <!-- By Jerome Etienne: http://jetienne.com/ -->

<script id="floorVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="myVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uvScale * uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="skyBoxVertShader" type="x-shader/x-vertex">
varying vec2 vUv;
void main()
{       
  vec4 vcsP = modelViewMatrix * vec4(position,1.0);
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vUv = uv;
}
</script>

<script id="holeyFragShader" type="x-shader/x-fragment">
uniform vec3 lightPosition;
uniform vec3 myColor;
varying vec3 vcsNormal;
varying vec3 vcsPosition;
varying vec3 ocsPosition;

void main( void ) {

  float x = ocsPosition.x;
  float y = ocsPosition.y;
  float z = ocsPosition.z;
  float c = 3.0;
  
  float dx = (x*c) - floor((x*c) + 0.5);
  float dy = (y*c) - floor((y*c) + 0.5);
  float dz = (z*c) - floor((z*c) + 0.5);
  float r2 = dx*dx+dy*dy+dz*dz;
  
  vec3 L = normalize(lightPosition - vcsPosition);
  vec3 N = normalize(vcsNormal);
  float i = max(0.0, dot(N,L));
  
  if (r2 < 0.3) {
  	discard;
  }
  else {
  	gl_FragColor = vec4(myColor * i, 1.0);
  }
  
}
</script>

<script id="toonFragShader" type="x-shader/x-fragment">
varying vec3 vcsNormal;
varying vec3 vcsPosition;
uniform vec3 lightPosition;
uniform vec3 myColor;

void main( void ) {
  vec3 L = normalize(lightPosition - vcsPosition);
  vec3 N = normalize(vcsNormal);
  float i = floor(max(0.0,dot(N,L))*5.0) / 5.0;

  gl_FragColor = vec4(myColor * i, 1.0);   // default is yellow
}
</script>




<script id="envmapFragShader" type="x-shader/x-fragment">
uniform vec3 lightPosition;
uniform sampler2D uPosxTexture;
uniform sampler2D uPosyTexture;
uniform sampler2D uPoszTexture;
uniform sampler2D uNegxTexture;
uniform sampler2D uNegyTexture;
uniform sampler2D uNegzTexture;
uniform vec3 myColor;
uniform mat4 matrixWorld;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;

void main( void ) {
  vec3 N = normalize(vcsNormal);
  vec3 I = normalize(vcsPosition);
  vec3 R = reflect(I,N);
  vec4 reflected = vec4(R, 1.0) * matrixWorld;
  
  //posx
  if(reflected.x > abs(reflected.y) && reflected.x > abs(reflected.z)) {
    float u = ((reflected.z/reflected.x)+1.0) / 2.0; 
    float v = ((reflected.y/reflected.x)+1.0) / 2.0;
    gl_FragColor = texture2D(uPosxTexture, vec2(u,v));
  }
  
  //negx
  else if (reflected.x < -abs(reflected.y) && reflected.x < -abs(reflected.z)) {
  	  float u = ((reflected.z / reflected.x) + 1.0) / 2.0;
  	  float v = (-(reflected.y / reflected.x) + 1.0) / 2.0;
  	  gl_FragColor = texture2D(uNegxTexture, vec2(u, v));
  }
  
  //posy
  else if (reflected.y > abs(reflected.x) && reflected.y > abs(reflected.z)) {
      float u = (-(reflected.x / reflected.y) + 1.0) / 2.0;
      float v = (-(reflected.z / reflected.y) + 1.0) / 2.0;
      gl_FragColor = texture2D(uPosyTexture, vec2(u, v));
  }

  //negy
  else if (reflected.y < -abs(reflected.x) && reflected.y < -abs(reflected.z)) {
  	  float u = ((reflected.x / reflected.y) + 1.0) / 2.0;
  	  float v = (-(reflected.z / reflected.y) + 1.0) / 2.0;
  	  gl_FragColor = texture2D(uNegyTexture, vec2(u, v));
  }
  
  //posz
  else if (reflected.z > abs(reflected.x) && reflected.z > abs(reflected.y)) {
	  float u = (-(reflected.x / reflected.z) + 1.0) / 2.0;
	  float v = ((reflected.y / reflected.z) + 1.0) / 2.0;
	  gl_FragColor = texture2D(uPoszTexture, vec2(u, v));
  }
  
  else {
	  float u = (-(reflected.x / reflected.z) + 1.0) / 2.0;
	  float v = (-(reflected.y / reflected.z) + 1.0) / 2.0;
	  gl_FragColor = texture2D(uNegzTexture, vec2(u, v));
  }
  
}	
</script>

<script id="floorFragShader" type="x-shader/x-fragment">
uniform vec3 lightPosition;
uniform vec3 myColor;
varying vec3 vcsPosition;
varying vec3 ocsPosition;
uniform sampler2D normalMap;
uniform sampler2D textureMap;
varying vec2 vUv;

void main( void ) {
  vec3 color = texture2D(textureMap, vUv).xyz;
  gl_FragColor = vec4(color,1.0);
}
</script>


<script id="pnoiseFragShader" type="x-shader/x-fragment">
float screenWidth = 2.0;
float PI = 3.1416;

// perlin noise code from: 
//   https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83

float rand(vec2 c){
	return fract(sin(dot(c.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float noise(vec2 p, float freq ){
	float unit = screenWidth/freq;
	vec2 ij = floor(p/unit);
	vec2 xy = mod(p,unit)/unit;
	//xy = 3.*xy*xy-2.*xy*xy*xy;
	xy = .5*(1.-cos(PI*xy));
	float a = rand((ij+vec2(0.,0.)));
	float b = rand((ij+vec2(1.,0.)));
	float c = rand((ij+vec2(0.,1.)));
	float d = rand((ij+vec2(1.,1.)));
	float x1 = mix(a, b, xy.x);
	float x2 = mix(c, d, xy.x);
	return mix(x1, x2, xy.y);
}

float pNoise(vec2 p, int res){
	float persistance = .5;
	float n = 0.;
	float normK = 0.;
	float f = 4.;
	float amp = 1.;
	int iCount = 0;
	for (int i = 0; i<50; i++){
		n+=amp*noise(p, f);
		f*=2.;
		normK+=amp;
		amp*=persistance;
		if (iCount == res) break;
		iCount++;
	}
	float nf = n/normK;
	return nf*nf*nf*nf;
}

varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main( void ) {
  int levels = 5;
//  float i = pNoise(vcsPosition.xy, levels);
  float i = pNoise(ocsPosition.xy,levels);
  float j = floor(i + 0.9);
  gl_FragColor = vec4(j, j, 0.3, 1.0);
}
// When the scene is being rotated, the scene in the box remains stationary
//When using vcs, the scene is moving along with the camera

//When increase the number of levels, the image gets more fine/detailed
//Does this mean more noise?
</script>

<script src="a5.js"></script>
</body>
</html>

